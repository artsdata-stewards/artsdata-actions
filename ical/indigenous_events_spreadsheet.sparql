PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
construct {
    ?event a schema:Event ;
           schema:name ?name ;
           schema:startDate ?startDate ;
           schema:url ?url ;
           schema:description ?desc ;
           schema:location ?loc ;
           schema:endDate ?endDate .
    ?loc schema:name ?loc_name ;
         schema:address ?address .
    ?address schema:streetAddress ?streetAddress ;
             schema:addressRegion ?addressRegion ;
             schema:addressCountry ?addressCountry ;
             schema:postalCode ?postalCode ;
             schema:addressLocality ?addressLocality .
}
where {
    graph <http://kg.artsdata.ca/capacoa/google-sheet/indigenous-performances> {
        ?event a schema:Event ;
               schema:name ?name ;
               schema:startDate ?startDate ;
               schema:url ?url ;
               schema:description ?desc ;
               schema:location ?loc .
    }
    ?loc schema:name ?loc_name ;
         schema:address ?address .
    ?address schema:streetAddress ?streetAddress ;
             schema:addressRegion ?addressRegion ;
             schema:addressCountry ?addressCountry ;
             schema:postalCode ?postalCode ;
             schema:addressLocality ?addressLocality .
    OPTIONAL {
        ?event schema:endDate ?endDate .
    }
    OPTIONAL {
          ?error sh:focusNode  ?event .
    }
    filter(!Bound(?error))
}
