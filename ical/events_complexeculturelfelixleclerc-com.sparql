PREFIX schema: <http://schema.org/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
construct {
    ?event a schema:Event ;
           schema:name ?name ;
           schema:startDate ?startDate ;
           schema:url ?url ;
           schema:description ?desc_with_image ;
           schema:location ?loc ;
           schema:endDate ?endDate .
    ?loc schema:name ?loc_name ;
         schema:address ?address .
    ?address schema:streetAddress ?streetAddress ;
             schema:addressRegion ?addressRegion ;
             schema:addressCountry ?addressCountry ;
             schema:postalCode ?postalCode ;
             schema:addressLocality ?addressLocality .
}
where {
    graph <http://kg.artsdata.ca/culture-creates/footlight/complexeculturelfelixleclerc-com> {
        ?event a schema:Event ;
               schema:name ?name ;
               schema:startDate ?startDate ;
               schema:url ?url ;
               schema:description ?desc ;
               schema:location ?loc .
    }
    ?loc schema:name ?loc_name ;
         schema:address ?address .
    ?address schema:streetAddress ?streetAddress ;
             schema:addressRegion ?addressRegion ;
             schema:addressCountry ?addressCountry ;
             schema:postalCode ?postalCode ;
             schema:addressLocality ?addressLocality .
    OPTIONAL {
        ?event schema:endDate ?endDate .
    }
     OPTIONAL {
        ?event schema:image ?image .
    }
    OPTIONAL {
        ?error sh:focusNode  ?event .
    }
    BIND(CONCAT(str(?desc),"   ", str(?image)) as ?desc_with_image)
    filter(?startDate > now())
    filter(!Bound(?error))
}
