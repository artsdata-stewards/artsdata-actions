PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?event
(GROUP_CONCAT(DISTINCT ?additionalTypeLabel;
        separator=", ") AS ?additionalTypes)
(GROUP_CONCAT(DISTINCT ?keyword;
        separator=", ") AS ?keywords)
(SAMPLE(?names) as ?name)
?startDate
?endDate
# ?location
(SAMPLE(?locationNames) as ?locationName)
(SAMPLE(?municipalities) as ?municipality) 
# ?postalCode
# ?organizer
(SAMPLE(?organizerNames) as ?organizerName)
WHERE {
    ?event a schema:Event ;
           schema:name ?names ;
           schema:startDate ?startDate ;
           schema:location ?location ;
           schema:organizer ?organizer .
    filter(?startDate > now())
    OPTIONAL {
        ?event  schema:endDate ?endDate .
    }
    #  FILTER(?location = <http://kg.artsdata.ca/resource/K2-237>)
    #  FILTER(?organizer IN (
    #        <http://kg.artsdata.ca/resource/K10-26>,
    #        <http://kg.artsdata.ca/resource/K14-29>,
    #        <http://kg.artsdata.ca/resource/K14-16>
    #    )
    #)
    OPTIONAL {
        ?event schema:additionalType ?additionalType .
        ?additionalType rdfs:label ?additionalTypeLabel .
    }
    OPTIONAL {
        ?event schema:keywords ?keyword .
    }
    OPTIONAL {
        ?location schema:name ?locationNames .
        ?location schema:address ?address .
        ?address schema:addressLocality ?municipalities ;
                 schema:postalCode ?postalCode .
    }
#    FILTER(
#        LCASE(?municipalities) = "ottawa" ||
#        LCASE(?municipalities) = "gatineau"
#    )
    FILTER(
        REGEX(?postalCode, "^K[1-4]", "i") ||
        REGEX(?postalCode, "^J8[L-Z]", "i") ||
        REGEX(?postalCode, "^J9[A-J]", "i")
    )
    OPTIONAL {
        ?organizer schema:name ?organizerNames .
    }
}
GROUP BY ?event  ?startDate ?endDate ?location  ?postalCode
ORDER BY ?startDate
