PREFIX schema: <http://schema.org/>

# This SPARQL lists events taking place in the province of Quebec that are linked to performers
select 
?event 
(sample(?event_names) as ?event_name)  
(sample(?fl_performers) as ?footlight_performer)
(sample(?wikidatas) as ?wikidata_performer)
(sample(?artsdatas) as ?artsdata_performer)
(sample(?region) as ?province)
where { 
   ?event schema:location/schema:address/schema:addressRegion ?region  .
    filter(lcase(str(?region)) = "qc" )
	graph <http://kg.artsdata.ca/core> {
        ?event a schema:Event ; schema:performer ?performers ; schema:name ?event_names .
   }
 BIND(if(contains(str(?performers),"footlight.io"),?performers,"") as ?fl_performers)
    BIND(if(contains(str(?performers),"wikidata.org"),?performers,"") as ?wd_performers)
   BIND(if(contains(str(?performers),"kg.artsdata.ca"),?performers,"") as ?ad_performers)
  
  OPTIONAL {
       ?performers schema:sameAs ?same_performers .
  }
    
    BIND(if(contains(str(?same_performers),"wikidata.org"),?same_performers,"") as ?wd_sames)
   BIND(if(contains(str(?same_performers),"kg.artsdata.ca"),?same_performers,"") as ?ad_sames)

 OPTIONAL {
       ?same_performers_sub schema:sameAs ?performers .
  }
   BIND(if(contains(str(?same_performers_sub),"wikidata.org"),?same_performers_sub,"") as ?wd_same_subs)
   BIND(if(contains(str(?same_performers_sub),"kg.artsdata.ca"),?same_performers_sub,"") as ?ad_same_subs)
 
  BIND(COALESCE(IF(?wd_performers != "",?wd_performers, ?wd_sames),?wd_same_subs) as ?wikidatas)
  BIND(COALESCE(IF(?ad_performers != "",?ad_performers, ?ad_sames),?ad_same_subs) as ?artsdatas)

  
} group by ?event 
