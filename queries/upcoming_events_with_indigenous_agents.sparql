PREFIX schema: <http://schema.org/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
select  ?event 
(sample(?names) as ?name) 
?startDate   
?endDate 
?performer 
(sample(?descs_trunc) as ?desc_trunc)  
(sample(?loc_names) as ?loc_name) 
(sample(?streetAddresss) as ?streetAddress) 
(sample(?addressLocalitys) as ?addressLocality)  
(sample(?addressRegions) as ?addressRegion)  
(sample(?addressCountrys) as ?addressCountry)  
?postalCode  
?url 
?error
where {
    # 1 Get indigenous people in Wikidata with Artsdata IDs
    {
        select distinct ?performer where {
            SERVICE <https://query.wikidata.org/sparql> {
                {
                    ?wdid_uri  wdt:P172/wdt:P279* wd:Q15571255 ;
                                                # indigenous peoples in North America
                                                wdt:P7627 ?adid .
                } UNION {
                    ?wdid_uri  wdt:P1552/wdt:P279* wd:Q15571255 ;
                                                 # indigenous peoples in North America
                                                 wdt:P7627 ?adid .
                }
            }
            bind(uri(concat("http://kg.artsdata.ca/resource/",?adid))  as ?performer)
        }
    }
    # 2 Get event properties needed for iCal
    ?event a schema:Event ;
           schema:performer/schema:sameAs* ?performer ;
           schema:name ?names ;
           schema:startDate ?startDate .
    OPTIONAL {
        ?event schema:url ?url_any
        bind(URI(?url_any) as ?url)
    }
    OPTIONAL {
        ?event schema:description ?desc .
        bind(CONCAT(substr(?desc,1,50),"...") as ?descs_trunc)
    }
    OPTIONAL {
        ?event schema:location ?loc .
        ?loc schema:name ?loc_names ;
             schema:address ?address .
        ?address schema:streetAddress ?streetAddresss ;
                 schema:addressRegion ?addressRegions ;
                 schema:addressCountry ?addressCountrys ;
                 schema:postalCode ?postalCode ;
                 schema:addressLocality ?addressLocalitys .
    }
    OPTIONAL {
        ?event schema:endDate ?endDate .
        filter(datatype(?endDate) = xsd:dateTime)
    }
    OPTIONAL {
        ?bn sh:focusNode  ?event .
        ?bn sh:resultSeverity ?error .
    }
    # filter(!Bound(?error))
    # Remove events linked to Artsdata minted event
    filter( not exists { 
        ?event ^schema:sameAs ?ad_event . 
        ?ad_event schema:performer ?some_performer .
        filter(contains(str(?ad_event),"kg.artsdata.ca/resource/"))  })
    filter(?startDate > now())
} group by ?event ?startDate   ?endDate ?performer   ?postalCode  ?url ?error
