PREFIX schema: <http://schema.org/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX onto: <http://www.ontotext.com/>

select  ?event 
(sample(?names) as ?name) 
?startDate   
?endDate 
(sample(?performer) as ?performer_) 
(sample(?organizer) as ?organizer_)
(sample(?desc) as ?description)
(sample(?loc_names) as ?place) 
(sample(?streetAddress) as ?Address) 
(sample(?addressLocality) as ?Locality)  
(sample(?addressRegion) as ?Region)  
(sample(?addressCountry) as ?Country)  
(sample(?postalCode) as ?postal) 
?error
# select reduced ?event ?names
from onto:explicit
where {
    # All events from Footlight CMS calendar indigenous-performances source
    {
        select ?event ?startDate ?endDate where {
            {
                graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-footlight/indigenous-performances> {
                    ?event a schema:Event ;
                    schema:startDate ?startDate .
                    filter(?startDate > now())
                }
            }
            UNION
            {
                {
                    # Get indigenous agents in Wikidata with Artsdata IDs
                    select distinct ?indigenousAgent where {
                        SERVICE <https://query.wikidata.org/sparql> {
                            {
                                ?person  wdt:P172/wdt:P279* wd:Q15571255 ;
                                # indigenous peoples in North America
                                wdt:P7627 ?adid .
                            } UNION {
                                ?organization  wdt:P1552/wdt:P279* wd:Q15571255 ;
                                # indigenous peoples in North America            
                                wdt:P7627 ?adid .
                            }
                            bind(uri(concat("http://kg.artsdata.ca/resource/",?adid))  as ?indigenousAgent)
                        }
                    }
                }
                # Events with indigenous performers
                ?event a schema:Event .
                ?event schema:organizer/schema:sameAs* | schema:performer/schema:sameAs*  ?indigenousAgent .
                graph ?g {
                    ?event schema:startDate ?startDate .
                    filter(?startDate > now())
                    # Exclude events from Indigenous CMS
                    filter(?g != <http://kg.artsdata.ca/culture-creates/artsdata-planet-footlight/indigenous-performances>)
                }
                # Exclude events that are sameAs events from Indigenous CMS
                FILTER NOT EXISTS {
                    graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-footlight/indigenous-performances> {
                        ?cms_event a schema:Event .
                    }
                    ?cms_event schema:sameAs ?event .
                }
                # Exclude event that are duplicates with Artsdata Minted Events
                FILTER NOT EXISTS {
                    graph <http://kg.artsdata.ca/core> {
                        ?event schema:sameAs ?ad_event .
                        filter(contains(str(?ad_event),"kg.artsdata.ca")) 
                    } 
                }
            }
        }
    }
    # 3 Get event properties needed for iCal
    ?event  schema:name ?names .
    OPTIONAL {
        ?event schema:endDate ?endDate .
        filter(datatype(?endDate) = xsd:dateTime)
    }
    # Get event URL
    OPTIONAL {
        ?event schema:url ?url .
    }
    OPTIONAL {
        ?event ^schema:subEvent/schema:url ?super_url .
    }
    BIND(COALESCE(?url, ?super_url) AS ?urls)
    OPTIONAL {
        ?event schema:description ?desc .
    }
    # Location
    ?event schema:location ?loc .
    ?loc schema:name ?loc_names ;
    schema:address ?address .
    OPTIONAL {
        ?loc schema:sameAs ?loc_sameAs .
    }
    ?address schema:streetAddress ?streetAddress ;
    schema:addressRegion ?addressRegion ;
    schema:addressCountry ?addressCountry ;
    schema:postalCode ?postalCode ;
    schema:addressLocality ?addressLocality .
    # Performer 
    OPTIONAL {
        ?event schema:performer ?performer .
        ?performer schema:name ?performerName ;
        a ?performerType .
        OPTIONAL {
            ?performer schema:sameAs ?performerSameAs .
        }
    }
    # Organizer 
    OPTIONAL {
        ?event schema:organizer ?organizer .
        ?organizer a ?organizerType ;
        schema:name ?organizerName .
        OPTIONAL {
            ?organizer schema:sameAs ?organizerSameAs .
        }
    }
    # Remove events with errors
    OPTIONAL {
        ?bn sh:focusNode ?event ;
        sh:resultSeverity ?error .
    }
    filter(!Bound(?error) || ?error != sh:Violation )
} group by ?event ?startDate ?endDate ?error ?url
