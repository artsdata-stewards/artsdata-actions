PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX luc: <http://www.ontotext.com/owlim/lucene#>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
select distinct ?event ?event_name  ?startDate ?linked_person_name ?score ?agent_name ?artsdata_agent ?type_sample ?wdid_uri  where {
    {
       
        select   (sample(?name) as ?agent_name)  ?artsdata_agent  (sample(?type) as ?type_sample)  ?wdid_uri where {
            SERVICE <https://query.wikidata.org/sparql> {
                {
                    ?wdid_uri  wdt:P172/wdt:P279* wd:Q15571255 ;
                                                # indigenous peoples in North America
                                                wdt:P7627 ?adid .
                } UNION {
                    ?wdid_uri  wdt:P1552/wdt:P279* wd:Q15571255 ;
                                                 # indigenous peoples in North America
                                                 wdt:P7627 ?adid .
                }
            }
            bind(uri(concat("http://kg.artsdata.ca/resource/",?adid))  as ?artsdata_agent)
            graph ?g {
                ?artsdata_agent a ?type ;
                              schema:name ?name .
            }
        } group by ?artsdata_agent  ?wdid_uri 
    }
    ?event luc:Name ?agent_name ;
           a schema:Event ;
           schema:startDate ?startDate ;
           luc:score ?score ;
           rdfs:label ?event_name .
    optional {
        ?event schema:performer/schema:name ?linked_person_name 
    }
    filter(?startDate > now())
    bind(strdt(?score,xsd:decimal) as ?score_decimal)
    filter(?score_decimal > 2.0)
   filter(?linked_person_name != ?agent_name )
   
} order by DESC(?score)
