# Find People in Wikidata missing an Artsdata ID

PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select ?ad_uri ?wd_uri ?ad_id (sample(?name) as ?sample_name) 

WHERE {
    { select * where {
        ?ad_uri a schema:Organization ; schema:sameAs ?sameAs .
        OPTIONAL{
            ?ad_uri schema:name ?name 
        }
        filter(contains(str(?sameAs),"wikidata.org"))
        filter(contains(str(?ad_uri),"kg.artsdata.ca/resource"))
        bind(strafter(str(?ad_uri),"kg.artsdata.ca/resource/") as ?ad_id)
        bind(URI(?sameAs) as ?wd_uri)
        } limit 1000
     }
    SERVICE <https://query.wikidata.org/sparql>
    {
        OPTIONAL {
            ?wd_uri   wdt:P7627   ?adid  
        }
    }
    filter(!BOUND(?adid ))
} group by ?ad_uri ?wd_uri ?ad_id
