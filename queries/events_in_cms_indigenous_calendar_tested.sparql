PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
select ?event 
(sample(?names) as ?name) 
(count(distinct ?urls) as ?url)
(count(distinct ?performer) as ?performers) 
(count(distinct ?wikidata_performer) as ?wikidata_performers) 
(count(distinct ?wikidata_organizer) as ?wikidata_organizers) 
(count(distinct ?address_complete) as ?address_filled) 
(IF( (count(distinct ?address_complete) > 0) && (count(distinct ?urls) > 0), true, false) as ?good_for_feed)
where {
    # 1 Get indigenous agents in Wikidata with Artsdata IDs
    graph <http://kg.artsdata.ca/culture-creates/artsdata-planet-footlight/indigenous-performances> {
        ?event a schema:Event .
        {
            select distinct ?indigenous_agent where {
                SERVICE <https://query.wikidata.org/sparql> {
                    {
                        ?wdid  wdt:P172/wdt:P279* wd:Q15571255 ;
                        # indigenous peoples in North America
                        wdt:P7627 ?adid .
                    } UNION {
                        ?wdid  wdt:P1552/wdt:P279* wd:Q15571255 ;
                        # indigenous peoples in North America            
                        wdt:P7627 ?adid .
                    }
                }
                bind(uri(concat("http://kg.artsdata.ca/resource/",?adid))  as ?indigenous_agent)
            }
        }
        OPTIONAL {
            ?event schema:performer/schema:sameAs ?indigenous_agent
            BIND(true as ?wikidata_performer)
        }
        OPTIONAL {
            ?event schema:organizer/schema:sameAs ?indigenous_agent
            BIND(true as ?wikidata_organizer)
        }
        OPTIONAL {
            ?event schema:performer ?performer
        }
        OPTIONAL {
            ?event schema:location ?location .
            ?location schema:address ?address .
            ?address schema:postalCode ?postalCode ;
            schema:streetAddress ?streetAddress ;
            schema:addressLocality ?addressLocality ;
            schema:addressRegion ?addressRegion ;
            schema:addressCountry ?addressCountry .
            BIND(true as ?address_complete)
        }
        ?event schema:name ?names .
        OPTIONAL {
            ?event schema:url | ^schema:subEvent/schema:url ?urls .
            BIND(true as ?url_present)
        }
    }
} group by ?event
