PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
select
(sample(?org_name) as ?org) 
?url 
?pages_crawled
?structured_data_score
?event_count
?view_events_link
?crawl_start
?crawl_end
?activity
?crawl
where {
    ?spiderfeed a schema:DataFeed ;
    schema:dataFeedElement ?url .
    ?org schema:url ?url ;
    schema:name ?org_name .
    ?crawl prov:used ?url ;
    a prov:Activity ;
    prov:startedAtTime ?crawl_start .
    OPTIONAL {
        ?crawl prov:endedAtTime ?crawl_end .
    }
    OPTIONAL {
        ?crawl schema:additionalProperty ?stat1 .
        ?stat1 schema:name "urlsCrawledCount" ;
        schema:value ?pages_crawled .
    }
    OPTIONAL {
        ?crawl schema:additionalProperty ?stat2 .
        ?stat2 schema:name "structuredScore" ;
        schema:value ?structured_data_score .
    }
    OPTIONAL {
        ?crawl schema:additionalProperty ?stat3 .
        ?stat3 schema:name "eventsLoadedCount" ;
        schema:value ?event_count_from_additional_property .
    }
    OPTIONAL {
        ?crawl schema:name ?activity .
    }
    OPTIONAL {
        ?artifact_dataset prov:wasGeneratedBy ?crawl ;
        <http://dataid.dbpedia.org/ns/core#artifact> ?artifact .
        OPTIONAL {
            ?artifact_dataset  <http://rdfs.org/ns/void#classPortion> ?portions .
            ?portions <http://rdfs.org/ns/void#class> schema:Event ;
            <http://rdfs.org/ns/void#entities> ?event_count_from_artifact .
        }
        OPTIONAL {
            ?graph prov:wasDerivedFrom/<http://xmlns.com/foaf/0.1/primaryTopic>  ?artifact_dataset .
        }
        BIND(CONCAT("<a href='/query/show?sparql=list_events&graph=",str(?graph),"'>View events</a>") as ?view_events)
        BIND(if(!bound(?view_events), CONCAT("<a href='/artifact/show?artifactUri=",str(?artifact),"'>Load using databus</a>"),?view_events) as ?view_events_link)
    }
    BIND(COALESCE(?event_count_from_additional_property,?event_count_from_artifact) as ?event_count) 
} group by ?url ?view_events_link ?pages_crawled ?structured_data_score ?crawl_start ?crawl_end  ?event_count ?activity ?crawl
