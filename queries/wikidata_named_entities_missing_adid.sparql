# Find recently minted Artsdata entities linked to Wikidata but missing their Artsdata ID in Wikidata 
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select ?ad_uri ?wd_uri ?ad_id ?type (sample(?name) as ?sample_name) ?generatedAtTime

WHERE {
    { select * where {
      VALUES ?type {schema:Places schema:Organization schema:Person }    
        ?ad_uri a ?type ; 
                schema:sameAs ?sameAs ; 
                prov:generatedAtTime ?generatedAtTime .
        OPTIONAL{
            ?ad_uri schema:name ?name 
        }
      	
        filter(contains(str(?sameAs),"wikidata.org"))
        filter(contains(str(?ad_uri),"kg.artsdata.ca/resource"))
        bind(strafter(str(?ad_uri),"kg.artsdata.ca/resource/") as ?ad_id)
        bind(URI(?sameAs) as ?wd_uri)
    } order by DESC(?generatedAtTime) limit 200
     }
    SERVICE <https://query.wikidata.org/sparql>
    {
        OPTIONAL {
            ?wd_uri   wdt:P7627   ?adid  
        }
    }
    filter(!BOUND(?adid ))
} group by ?ad_uri ?wd_uri ?ad_id ?type ?generatedAtTime
