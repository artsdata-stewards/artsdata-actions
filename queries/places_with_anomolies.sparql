PREFIX schema: <http://schema.org/>

select  ?place  
(sample(?names) as ?name)
(count(distinct ?address) as ?address_count)  
(count(distinct ?addressLocality) as ?addressLocality_count) 
(count(distinct ?postalCode) as ?postalCode_count) 
(count(distinct ?addressRegion) as ?addressRegion_count) 
(sample(?addressCountry) as ?country)
(count(distinct ?addressCountry) as ?addressCountry_count) 
(count(distinct ?streetAddress) as ?streetAddress_count) 
where {
    graph <http://kg.artsdata.ca/core>  {
        ?place a schema:Place .
        filter(isURI(?place))
        OPTIONAL {
            ?place schema:address ?address .
        }
        OPTIONAL {
            ?place schema:name ?names .
        }
    }
    FILTER not exists { ?top schema:workLocation ?place }
    OPTIONAL {
        ?place schema:address ?address .
        OPTIONAL {
            ?address schema:postalCode ?postalCode .
        }
        OPTIONAL {
            ?address schema:addressLocality ?addressLocality .
        }
        OPTIONAL {
            ?address schema:addressRegion ?addressRegion .
            FILTER(strlen(str(?addressRegion)) > 2)
        }
        OPTIONAL {
            ?address schema:addressCountry ?addressCountry .
            FILTER(strlen(str(?addressRegion)) > 2)
        }
        OPTIONAL {
            ?address schema:streetAddress ?streetAddress .
        }
        OPTIONAL {
            ?place schema:streetAddress ?streetAddress .
        }
    }
    filter(!bound(?address) || !bound(?postalCode) || !bound(?addressLocality) || !bound(?addressRegion) || !bound(?addressCountry) || !bound(?streetAddress))
} group by ?place  
having (
    (count(?address) > 1 || count(?address) = 0)  ||
    (count(?postalCode) > 1 || count(?postalCode) = 0) ||
    count(?addressLocality) = 0 ||
    count(?addressRegion) = 0 ||
    count(?addressCountry) = 0 ||
    count(?streetAddress) = 0 
)
