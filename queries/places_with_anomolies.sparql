PREFIX schema: <http://schema.org/>
PREFIX adm: <http://kg.artsdata.ca/minted/>
select  ?place  
(count(distinct ?address) as ?address_count)  
(count(distinct ?addressLocality) as ?addressLocality_count) 
(count(distinct ?postalCode) as ?postalCode_count) 
(count(distinct ?addressRegion) as ?addressRegion_count) 
(count(distinct ?addressCountry) as ?addressCountry_count) 
(count(distinct ?streetAddress) as ?streetAddress_count) 
where {
    values ?g {
        adm:K2  adm:K4 adm:K5   adm:K11  adm:K13  adm:K16 
    }
    graph ?g {
        ?place a schema:Place .
        OPTIONAL {
            ?place schema:address ?address .
        }
    }
    # ?place schema:name  ?name . 
    OPTIONAL {
        ?place schema:address ?address .
        OPTIONAL {
            ?address schema:postalCode ?postalCode .
        }
        OPTIONAL {
            ?address schema:addressLocality ?addressLocality .
        }
        OPTIONAL {
            ?address schema:addressRegion ?addressRegion .
        }
        OPTIONAL {
            ?address schema:addressCountry ?addressCountry .
        }
        OPTIONAL {
            ?address schema:streetAddress ?streetAddress .
        }
        OPTIONAL {
            ?place schema:streetAddress ?streetAddress .
        }
    }
    filter(!bound(?address) || !bound(?postalCode) || !bound(?addressLocality) || !bound(?addressRegion) || !bound(?addressCountry) || !bound(?streetAddress))
} group by ?place  
having (
    (count(?address) > 1 || count(?address) = 0)  ||
    (count(?postalCode) > 1 || count(?postalCode) = 0) ||
    count(?addressLocality) = 0 ||
    count(?addressRegion) = 0 ||
    count(?addressCountry) = 0 ||
    count(?streetAddress) = 0 
)
