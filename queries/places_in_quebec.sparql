PREFIX schema: <http://schema.org/>

select  
?Artsdata  
?name 
?contained_in  
?wikidata  
(sample(?streetAddresses) as ?streetAddress) 
(sample(?localities) as ?locality) 
(sample(?regions) as ?region) 
(sample(?postalCodes) as ?postalCode)
(sample(?countries) as ?country)
where {
    ?Artsdata a schema:Place .
    OPTIONAL {
        ?Artsdata schema:containedInPlace ?contained_in .
        filter(contains(str(?contained_in),"http://kg.artsdata.ca/resource/K"))
    }
    OPTIONAL {
        ?Artsdata schema:name ?name_fr .
        filter(lang(?name_fr) = "fr")
    }
    OPTIONAL {
        ?Artsdata schema:name ?name_en .
        filter(lang(?name_en) = "en")
    }
    OPTIONAL {
        ?Artsdata schema:name ?name_none .
        filter(lang(?name_none) = "")
    }
    bind(coalesce(?name_fr,?name_none,?name_en) as ?name)
    filter(contains(str(?Artsdata),"http://kg.artsdata.ca/resource/K"))
    OPTIONAL {
        ?Artsdata schema:sameAs ?wikidata .
        filter(contains(str(?wikidata),"http://www.wikidata.org/entity/Q"))
    }
    ?Artsdata schema:address ?address .
    ?address schema:streetAddress ?streetAddresses .
    ?address  schema:addressRegion ?regions .
    OPTIONAL {
        ?address schema:addressCountry ?countries ;
                 schema:addressLocality ?localities ;
                 schema:postalCode ?postalCodes .
    }
   
    filter(str(?regions) = "QC" || str(?regions) = "Quebec" || str(?regions) = "Qu√©bec")
}
group by  ?name ?Artsdata   ?wikidata ?contained_in
