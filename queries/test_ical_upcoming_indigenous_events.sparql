PREFIX schema: <http://schema.org/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX sh: <http://www.w3.org/ns/shacl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
select distinct ?event (sample(?name) as ?event_name) ?startDate ?desc ?loc (sample(?loc_name) as ?location_name) ?url
where {
    # 1 Get indigenous people in Wikidata with Artsdata IDs
    {
        select distinct ?indigenousPerson where {
            SERVICE <https://query.wikidata.org/sparql> {
                {
                    ?person  wdt:P172/wdt:P279* wd:Q15571255 ;
                                              # indigenous peoples in North America
                                              wdt:P7627 ?adid .
                } UNION {
                    ?wdid_uri  wdt:P1552/wdt:P279* wd:Q15571255 ;
                                                 # indigenous peoples in North America            
                                                 wdt:P7627 ?adid .
                }
                bind(uri(concat("http://kg.artsdata.ca/resource/",?adid))  as ?indigenousPerson)
            }
        }
    }
    # 2 Get event properties needed for iCal
    ?event a schema:Event ;
           schema:performer ?indigenousPerson ;
           schema:name ?name ;
           schema:startDate ?startDate ;
           schema:url ?url ;
           schema:location ?loc .
    ?loc schema:name ?loc_name ;
         schema:address ?address .
    ?address schema:streetAddress ?streetAddress ;
             schema:addressRegion ?addressRegion ;
             schema:addressCountry ?addressCountry ;
             schema:postalCode ?postalCode ;
             schema:addressLocality ?addressLocality .
    OPTIONAL {
       ?event schema:description ?desc .
    }
    OPTIONAL {
        ?event schema:endDate ?endDate .
        filter(datatype(?endDate) = xsd:dateTime)
    }
    OPTIONAL {
        ?error sh:focusNode  ?event .
    }
    filter(!Bound(?error))
    filter( not exists {
            ?event ^schema:sameAs ?ad_event .
            filter(contains(str(?ad_event),"kg.artsdata.ca/resource/"))  
        })
    filter(?startDate > now())
} group by ?event ?startDate ?desc ?loc ?url
